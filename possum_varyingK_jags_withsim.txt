model {

for (i in 1:N.pops) { # loop over the 4 populations (rows of the data)
  for (j in 2:N.years) { # loop over years 2-11 (columns of the data)

    N[i,j] ~ dpois(N.hat[i,j]) # Poisson likelihood for population counts
    N.hat[i,j] <- N[i,j-1] * exp(r*(1-N[i,j-1]/K[i])) 
              # expected value of Poisson for next year, based on current pop size and 
              # logistic population growth rate
  }
}

# Priors
r ~ dunif(0, 1.0986) # Follow McCarthy and strongly constrain max growth rate
for (i in 1:N.pops) { K[i] ~ dunif(1, 50) } # priors for carrying capacity for each population

# simulate new data
for (i in 1:N.pops) {
  for(j in 2:N.years) {
    N.new.mean[i,j] <- N[i,j-1] * exp(r*(1-N[i,j-1]/K[i]))
    N.new[i,j] ~ dpois(N.new.mean[i,j])
  }
}

}